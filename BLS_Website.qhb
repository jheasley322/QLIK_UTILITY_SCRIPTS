//insert this line to call this script:
//*************************************************************************************************
//     $(Must_Include='lib://Qlik Network Folders\Application Scripts\HybridApps\BLS_Website.qhb');
//*************************************************************************************************


//Variables--------------------------------------------------
    SET ThousandSep=',';
    SET DecimalSep='.';
    SET MoneyThousandSep=',';
    SET MoneyDecimalSep='.';
    SET MoneyFormat='$#,##0.00;-$#,##0.00';
    SET TimeFormat='h:mm:ss TT';
    SET DateFormat='M/D/YYYY';
    SET TimestampFormat='M/D/YYYY h:mm:ss[.fff] TT';
    SET FirstWeekDay=6;
    SET BrokenWeeks=1;
    SET ReferenceDay=0;
    SET FirstMonthOfYear=1;
    SET CollationLocale='en-US';
    SET CreateSearchIndexOnReload=1;
    SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
    SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
    SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
    SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';

    let vStartYear = 2008;
//-----------------------------------------------------------


//Website Load-----------------------------------------------
    BLS:
    LOAD
        series_id,
        "year",
        period,
        value
        // footnotes
    FROM [lib://Transportation BLS]
    (URL is [https://download.bls.gov/pub/time.series/pc/pc.data.39.TruckTransportation], txt, codepage is 28592, embedded labels, delimiter is '\t', msq)
    Where Match(series_id,
    'PCU4841224841221',
    'PCU4841214841212',
    'PCU482111482111412',
    'PCU482111482111411');


    Concatenate
    LOAD
        series_id,
        "year",
        period,
        value
        // footnotes
    FROM [lib://Transportation BLS]
    (url is [https://download.bls.gov/pub/time.series/pc/pc.data.37.RailTransportation],txt, codepage is 28592, embedded labels, delimiter is '\t', msq)
    Where Match(series_id,
    'PCU4841224841221',
    'PCU4841214841212',
    'PCU482111482111412',
    'PCU482111482111411');

    Store BLS Into [lib://Qlik Network Folders/1_Source_QVD/BLS_RAW.qvd](qvd);

    Drop Table BLS;

//-----------------------------------------------------------


//Temp Transform 1-------------------------------------------
    Series:
    Mapping Load * Inline [
    Series ID,Index
    PCU4841224841221,LTL
    PCU4841214841212,Truckload
    PCU482111482111412,Intermodal   
    PCU482111482111411,Rail
    ];

    BLSTMP:
    Load
        Date#(period,'YYYYMMDD') as period1,
        *;
    LOAD
    //     series_id,
        Applymap('Series',series_id,'Not Found') as Index,
        "year",
    //     period,
        year&(PurgeChar(period,'M'))&'01' as period,
        value
        // footnotes
    FROM [lib://Qlik Network Folders/1_Source_QVD\BLS_RAW.qvd]
    (qvd)
    where period <> 'M13'
    AND year>= $(vStartYear)
    ;

    BLS_2:
    NoConcatenate
    Load	
        *
    Resident BLSTMP
    Order by Index,period1 asc;
    Drop Table BLSTMP;
//-----------------------------------------------------------


//Final BLS Table--------------------------------------------
    BLS_F:
    NoConcatenate
    Load
        If(Not Match(Left(period1,6),200801),value-Previous(value),null()) as delta,
        If(Not Match(Left(period1,6),200801),(value-Previous(value))/Previous(value),null()) as percent_delta,
        *
    Resident BLS_2;
    Drop Table BLS_2;

    Store BLS_F into [lib://Qlik Network Folders/1_Source_QVD\BLS_FINAL.qvd](qvd);


    BLSAvg:
    LOAD
    //     series_id,
        Applymap('Series',series_id,'Not Found') as Index,
    //     "year",
    //     period,
    //     Date#(year&(PurgeChar(period,'M')),'YYYYMM') as period1,
        value as YearlyAvg,
        value-Previous(value) as YearlyChange
    //     footnotes
    FROM [lib://Qlik Network Folders/1_Source_QVD\BLS_RAW.qvd]
    (qvd)
    where period = 'M13'
    AND year>= $(vStartYear);
//-----------------------------------------------------------